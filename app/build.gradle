plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.example.samplemoviedbcompose"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion rootProject.composeVersion
        kotlinCompilerVersion rootProject.kotlinVersion
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    configurations {
        ktlint
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    kapt "com.google.dagger:hilt-compiler:$rootProject.daggerVersion"

    implementation "androidx.activity:activity-compose:$rootProject.activityComposeVersion"
    implementation "androidx.compose.material:material:$rootProject.composeVersion"
    implementation "androidx.compose.material:material-icons-extended:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui:$rootProject.composeVersion"
    implementation "androidx.core:core-ktx:$rootProject.coreKtxVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$rootProject.hiltNavigationComposeVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleRuntimeKtxVersion"
    implementation "androidx.navigation:navigation-compose:$rootProject.composeNavigationVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "com.google.dagger:hilt-android:$rootProject.daggerVersion"
    implementation "com.squareup.logcat:logcat:$rootProject.logcatVersion"
    implementation "io.coil-kt:coil:$rootProject.coilVersion"
    implementation "io.coil-kt:coil-compose:$rootProject.coilVersion"

    ktlint "com.pinterest:ktlint:$rootProject.ktlintVersion"

    debugImplementation "androidx.compose.ui:ui-tooling:$rootProject.composeVersion"

    testImplementation "junit:junit:$rootProject.jUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.extjUnitVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$rootProject.composeVersion"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/reports/ktlint.xml"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/reports/ktlint.xml"
}
